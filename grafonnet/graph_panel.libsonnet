{
    new(
        title,
        span=12,
        id=null,
        fill=1,
        linewidth=1,
        min_span=null,
        format="short",
        min=null,
        max=null,
        lines=true,
        points=false,
        bars=false,
        dashes=false,
        stack=false,
        repeat=null,
    ):: {
        [if id != null then "id"]: id,
        title: title,
        span: span,
        [if min_span != null then "minSpan"]: min_span,
        type: "graph",
        datasource: null,
        targets: [
        ],
        renderer: "flot",
        yaxes: [
            $.yaxe(format, min, max),
            $.yaxe(format, min, max),
        ],
        xaxis: {
            show: true,
            mode: "time",
            name: null,
            values: [],
            buckets: null,
        },
        lines: lines,
        fill: fill,
        linewidth: linewidth,
        dashes: dashes,
        dashLength: 10,
        spaceLength: 10,
        points: points,
        pointradius: 5,
        bars: bars,
        stack: stack,
        percentage: false,
        legend: {
            show: true,
            values: false,
            min: false,
            max: false,
            current: false,
            total: false,
            avg: false,
        },
        nullPointMode: "null",
        steppedLine: false,
        tooltip: {
            value_type: "individual",
            shared: true,
            sort: 0,
        },
        timeFrom: null,
        timeShift: null,
        aliasColors: {},
        repeat: repeat,
        seriesOverrides: [],
        thresholds: [],
    },
    yaxe(
        format="short",
        min=null,
        max=null,
        label=null,
        show=true,
        logBase=1
    ):: {
        label: label,
        show: show,
        logBase: logBase,
        min: min,
        max: max,
        format: format,
    },
    addTarget(target):: {
        targets+: [target],
    },
}
